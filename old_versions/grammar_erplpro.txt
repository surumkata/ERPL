start         : atrbs? sala sons? eventos?

atrbs         : atrb+
atrb          : ARG "=" value

sala          : "S""(" ID "," tamanho ")""{" cenas "}"
cenas         : cena ("," cena)*
cena          : "C""(" ID ")""{" estobjs "}"
estobjs       : (estado|objeto) ("," (estado|objeto))*
objeto        : "O""(" ID ("," posicao "," tamanho)? ")""{" estados "}"

estados       : estado ("," estado)*
estado        : "E" INICIAL? "(" ID "," filename ("," posicao "," tamanho)? ")"
              | "E" INICIAL? "animado" "(" numero  "," numero ")" "(" ID "," "[" filenames "]" ("," posicao "," tamanho)? ")" -> estado_animado_lista
              | "E" INICIAL? "animado" "(" numero "," numero ")" "(" ID "," "glob" "(" text ")" REVERSE? ("," posicao "," tamanho)? ")" -> estado_animado_glob
              | "E" INICIAL? "(" ID "," "image_plus_txt" "(" filename "," filename "," text "," filename ("," color)? ")" ("," posicao "," tamanho)? ")" -> estado_img_plus_text

sons          : "M" "{" som ("," som)* "}"
som           : ID "(" filename ")"

eventos       : "E{" evento+ "}"
evento        : ID "(" numero? ")" "{" precondicoes "}" "=>" "{" poscondicoes "}" -> evento_simples
              | ID "(""-"")" "{""}" "=>" "{" poscondicoes "}"                  -> evento_ligado


precondicoes  : precondicao                             -> precondicoes_simples
              | precondicoes "&&" precondicoes          -> precondicoes_e
              | precondicoes "||" precondicoes          -> precondicoes_ou
              | "~" precondicoes                        -> precondicoes_nao
              | "(" precondicoes ")"                    -> precondicoes_grupo

precondicao   : "clique" "(" ID ")"                     -> clique
              | "clique_não" "(" ID ")"                 -> clique_nao
              | ID "@" ID                               -> esta_estado
              | "clique_depois" "(" ID "," ID ")"       -> clique_depois
              | "uso" "(" ID ")"                        -> uso

poscondicoes  : poscondicao ("&&" poscondicao)*
poscondicao   : ID "@" ID                               -> muda_estado
              | ID ">>" "inventário"                    -> vai_inv
              | "fim""("")"                             -> fim_de_jogo
              | "mensagem""("text","posicao")"         -> mensagem   
              | "pede_código" "(" text "," text "," posicao "," ID "," ID ")" -> pede_codigo
              | ID ">>" tamanho                         -> muda_tamanho
              | ID ">>" posicao                         -> muda_posicao
              | "cena" "." ID                           -> muda_cena
              | "remove" "(" ID ")"                     -> remove
              | "toca" "(" ID ")"                       -> toca_som

posicao       : posicao_value
              | ARG          -> posicao_arg
posicao_value : "(" par ")"
tamanho       : tamanho_value
              | ARG          -> tamanho_arg
tamanho_value : "[" par "]"
par           : numero "," numero
filenames     : filename ("," filename)*
filename      : text
text          : TEXTO
              | ARGN -> text_argn
              | ARG -> text_arg

numero        : NUM
              | ARG -> num_arg

color         : "(" numero "," numero "," numero ")"

value         : TEXTO                            -> value_text
              | posicao_value                    -> value_posicao
              | tamanho_value                    -> value_tamanho
              | NUM                              -> value_num

INICIAL : /i/
REVERSE : /\.reverse\(\)/

ID            : /[\w\-_]+/
TEXTO         : /"[^"]*"/
NUM           : /\d+/
ARGN          : /\$\d+/
ARG           : /\$[a-zA-Z]\w+/

%import common.WS
%ignore WS
start         : atrbs? sala sons? eventos? 

atrbs         : atrb+
atrb          : ARG "=" value

sala          : "Sala" ID tamanho "{" cenas "}"
cenas         : ("contém" cena)+
cena          : "Cena" ID "{" estobjs "}"
estobjs       : (("tem" estado)|("contém" objeto))+
objeto        : "Objeto" ID (posicao tamanho)? "{" estados "}"

estados       : ("tem" estado)+
estado        : "Estado" INICIAL? ID filename (posicao tamanho)?
              | "Estado" INICIAL? "animado" "(" numero "," numero ")" ID "[" filenames "]" (posicao tamanho)?  -> estado_animado_lista
              | "Estado" INICIAL? "animado" "(" numero "," numero ")" ID "glob" "(" text ")" REVERSE? (posicao tamanho)? -> estado_animado_glob
              | "Estado" INICIAL? ID "image_plus_txt" "(" filename "," filename "," text "," filename ("," color)? ")" (posicao tamanho)? -> estado_img_plus_text

sons          : "#Sons:" som+
som           : "Som" ID ":" filename

eventos       : "#Eventos:" evento+
evento        : "Evento" ID "(" numero? ")" ":" ("se"|"Se") precondicoes "," ("Então"|"então") poscondicoes "." -> evento_simples
              | "Evento" ID "(""-"")" ":" poscondicoes "." -> evento_ligado

precondicoes  : precondicao                             -> precondicoes_simples
              | precondicoes "e" precondicoes           -> precondicoes_e
              | precondicoes "ou" precondicoes          -> precondicoes_ou
              | "não" precondicoes                      -> precondicoes_nao
              | "(" precondicoes ")"                    -> precondicoes_grupo

precondicao   : "clique" ID                                     -> clique
              | "clique não" ID                                 -> clique_nao
              | ID "está" ID                                    -> esta_estado
              | "clique" ID "depois de" ID "ter acontecido"     -> clique_depois
              | ID "está em uso"                                -> uso

poscondicoes  : poscondicao ("e" poscondicao)*
poscondicao   : ID "muda para" ID                                                                   -> muda_estado
              | ID "vai para o inventário"                                                          -> vai_inv
              | "fim de jogo"                                                                       -> fim_de_jogo
              | "mostra mensagem" text "em" posicao                                                -> mensagem   
              | "pede código" text "com mensagem" text "em" posicao ", se acertar faz" ID "e se errar faz" ID  -> pede_codigo
              | "arrasta" ID ", se deixar em cima de" ID "faz" ID "e se não faz" ID                     -> arrasta_objeto
              | ID "muda tamanho para" tamanho                                                      -> muda_tamanho
              | ID "muda posição para" posicao                                                      -> muda_posicao
              | "muda para cena" ID                                                                 -> muda_cena
              | ID "é removid" ("o"|"a")                                                            -> remove
              | "toca" ID                                                                           -> toca_som

posicao       : posicao_value
              | ARG          -> posicao_arg
posicao_value : "(" par ")"
tamanho       : tamanho_value
              | ARG          -> tamanho_arg
tamanho_value : "[" par "]"
par           : numero "," numero
filenames     : filename ("," filename)*
filename      : text
text          : TEXTO
              | ARGN -> text_argn
              | ARG -> text_arg

numero        : NUM
              | ARG -> num_arg

color         : "(" numero "," numero "," numero ")"

value         : TEXTO                            -> value_text
              | posicao_value                    -> value_posicao
              | tamanho_value                    -> value_tamanho
              | NUM                              -> value_num


INICIAL : /inicial/
REVERSE : /\.reverse\(\)/

ID            : /[\w\-_]+/
TEXTO         : /"[^"]*"/
NUM           : /\d+/
ARGN          : /\$\d+/
ARG           : /\$[a-zA-Z]\w+/

%import common.WS
%ignore WS
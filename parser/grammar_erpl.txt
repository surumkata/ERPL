start         : sala sons? eventos?

sala          : "Sala" ID tamanho cenas
cenas         : ("contém" cena)+
cena          : "Cena" ID "{" estobjs "}"
estobjs       : (("tem" estado)|("contém" objeto))+
objeto        : "Objeto" ID (posicao tamanho)? "{" estados "}"

estados       : ("tem" estado)+
estado        : "Estado" INICIAL? ID filename (posicao tamanho)?
              | "Estado" INICIAL? "animado" "(" NUM  "," NUM ")" ID "[" filenames "]" (posicao tamanho)? -> estado_animado

sons          : "#Sons:" som+
som           : "Som" ID ":" filename

eventos       : "#Eventos:" evento+
evento        : "Evento" ID "(" NUM? ")" ":" ("se"|"Se") precondicoes "," ("Então"|"então") poscondicoes "." -> evento_simples
              | "Evento" ID "(""-"")" ":" poscondicoes "." -> evento_ligado

precondicoes  : precondicao                             -> precondicoes_simples
              | precondicoes "e" precondicoes           -> precondicoes_e
              | precondicoes "ou" precondicoes          -> precondicoes_ou
              | "não" precondicoes                      -> precondicoes_nao
              | "(" precondicoes ")"                    -> precondicoes_grupo

precondicao   : "clique" ID                                     -> clique
              | "clique não" ID                                 -> clique_nao
              | ID "está" ID                                    -> esta_estado
              | "clique" ID "depois de" ID "ter acontecido"     -> clique_depois
              | ID "está em uso"                                -> uso

poscondicoes  : poscondicao ("e" poscondicao)*
poscondicao   : ID "muda para" ID                                                                   -> muda_estado
              | ID "vai para o inventário"                                                          -> vai_inv
              | "fim de jogo"                                                                       -> fim_de_jogo
              | "mostra mensagem" TEXTO "em" posicao                                                -> mensagem   
              | "pede código" TEXTO "com mensagem" TEXTO "em" posicao ", se acertar faz" ID "e se errar faz" ID  -> pede_codigo
              | ID "muda tamanho para" tamanho                                                      -> muda_tamanho
              | ID "muda posição para" posicao                                                      -> muda_posicao
              | "muda para cena" ID                                                                 -> muda_cena
              | ID "é removid" ("o"|"a")                                                            -> remove
              | "toca" ID                                                                           -> toca_som

posicao       : "(" par ")"
tamanho       : "[" par "]"
par           : NUM "," NUM
filenames     : filename ("," filename)*
filename      : TEXTO

INICIAL : /inicial/


ID            : /[\w\-_]+/
TEXTO         : /"[^"]*"/
NUM           : /\d+/

%import common.WS
%ignore WS
start         : sala sons? eventos?

sala          : "S""(" ID "," tamanho ")""{" cenas "}"
cenas         : cena ("," cena)*
cena          : "C""(" ID ")""{" estobjs "}"
estobjs       : (estado|objeto) ("," (estado|objeto))*
objeto        : "O""(" ID ("," posicao "," tamanho)? ")""{" estados "}"

estados       : estado ("," estado)*
estado        : "E" INICIAL? "(" ID "," filename ("," posicao "," tamanho)? ")"
              | "E" INICIAL? "animado" "(" NUM  "," NUM ")" "(" ID "," "[" filenames "]" ("," posicao "," tamanho)? ")" -> estado_animado

sons          : "M" "{" som ("," som)* "}"
som           : ID "(" filename ")"

eventos       : "E{" evento+ "}"
evento        : ID "(" NUM? ")" "{" precondicoes "}" "=>" "{" poscondicoes "}" -> evento_simples
              | ID "(""-"")" "{""}" "=>" "{" poscondicoes "}"                  -> evento_ligado


precondicoes  : precondicao                             -> precondicoes_simples
              | precondicoes "&&" precondicoes          -> precondicoes_e
              | precondicoes "||" precondicoes          -> precondicoes_ou
              | "~" precondicoes                        -> precondicoes_nao
              | "(" precondicoes ")"                    -> precondicoes_grupo

precondicao   : "clique" "(" ID ")"                     -> clique
              | "clique_não" "(" ID ")"                 -> clique_nao
              | ID "@" ID                               -> esta_estado
              | "clique_depois" "(" ID "," ID ")"       -> clique_depois
              | "uso" "(" ID ")"                        -> uso

poscondicoes  : poscondicao ("&&" poscondicao)*
poscondicao   : ID "@" ID                               -> muda_estado
              | ID ">>" "inventário"                    -> vai_inv
              | "fim""("")"                             -> fim_de_jogo
              | "mensagem""("TEXTO","posicao")"         -> mensagem   
              | "pede_código" "(" TEXTO "," TEXTO "," ID "," ID ")" -> pede_codigo
              | ID ">>" tamanho                         -> muda_tamanho
              | ID ">>" posicao                         -> muda_posicao
              | "cena" "." ID                           -> muda_cena
              | "remove" "(" ID ")"                     -> remove
              | "toca" "(" ID ")"                       -> toca_som

posicao       : "(" par ")"
tamanho       : "[" par "]"
par           : NUM "," NUM
filenames     : filename ("," filename)*
filename      : TEXTO

INICIAL : /i/

ID            : /[\w\-_]+/
TEXTO         : /"[^"]*"/
NUM           : /\d+/

%import common.WS
%ignore WS
$room = "room.png"
$tamanho = [225,300]
$posicao = (1000,485)
$num = 100
$codigo = "932"

Sala sala [1300,700] {
    contém Cena cena {
        tem Estado inicial background $room
        contém Objeto porta (600,342) $tamanho {
            tem Estado inicial fechada "door.png"
            tem Estado animado(3,1) aberta ["door/door1.png","door/door2.png","door/door3.png","door/door4.png","door/door5.png"]
        }
        contém Objeto porta2 (0,342) $tamanho {
            tem Estado inicial fechada "door/door0.png"
            tem Estado animado(3,1) aberta glob("doort/*.png")
        }
        contém Objeto cofre $posicao [172,$num] {
            tem Estado inicial fechado "cofre.png"
            tem Estado aberto "open_cofre.png"
        }
        contém Objeto chave (1020,520) [50,25] {
            tem Estado normal "key.png"
            tem Estado ativa "active_key.png"
        }   
        contém Objeto nota {
            tem Estado inicial normal "nota.png" (400,600) [30,30]
        }
        contém Objeto nota_lupa (300,500) [200,200] {
            tem Estado visivel image_plus_txt("nota.png","nota_nova.png",$codigo,"arial.ttf",(255,255,255)) 
        }
        contém Objeto lupa (200,500) [40,40] {
            tem Estado inicial normal "lupa.png"
            tem Estado ativa "active_lupa.png"
        }
    }
    contém Cena cena2 {
        tem Estado inicial background "room.png"
        contém Objeto porta3 (600,342) [225,300] {
            tem Estado inicial aberta "open_door.png"
        }
    }
}

#Sons:

Som porta_a_abrir: "door-1-open.mp3"
Som porta-locked: "door-locked.mp3"

#Eventos:

Evento abrir_porta(1):
Se clique porta e porta está fechada e chave está em uso,
então porta muda para aberta e chave é removida e toca porta_a_abrir.

Evento abrir_porta2(1):
Se clique porta2 e porta2 está fechada,
então porta2 muda para aberta e toca porta_a_abrir.

Evento muda_cena():
Se clique porta2 e porta2 está aberta,
então muda para cena cena2.

Evento muda_cena2():
Se clique porta3 e porta3 está aberta,
então muda para cena cena.

Evento fim(1):
se clique porta e porta está aberta,
então fim de jogo.

Evento chave_inventario(1):
Se clique chave e cofre está aberto,
Então chave vai para o inventário.

Evento tentar_abrir_cofre():
Se clique cofre e cofre está fechado,
então pede código $codigo com mensagem "Qual é o código do cofre?" em (1000, 485) , se acertar faz abrir_cofre e se errar faz mostrar_erro.

Evento abrir_cofre(-):
cofre muda para aberto e chave muda para normal.

Evento mostrar_erro(-):
mostra mensagem "Código errado!" em (1000, 485).

Evento mensagem_trancada():
Se clique porta e porta está fechada e não chave está em uso,
então mostra mensagem "porta trancada! procura uma chave!" em (600,385) e toca porta-locked.

Evento arrasta_lupa():
Se clique lupa,
Então arrasta lupa, se deixar em cima de nota faz aumentar_nota e se não faz move_lupa. 

Evento aumentar_nota(-):
nota_lupa muda para visivel.

Evento move_lupa(-):
lupa muda posição para (200,500).

Evento desaparecer_nota_lupa():
Se clique não nota_lupa e nota_lupa está visivel,
então nota_lupa muda para null.